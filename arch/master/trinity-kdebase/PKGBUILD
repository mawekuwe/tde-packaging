# $Id$
# Maintainer: David C. Rankin <drankinatty@gmail.com>
#
# All modifications and uses of this file are licensed under
# the software for which this file was made for, should the software
# be under an Open Source License, at least version 1.9, defined
# by the Open Source Initiative. In other cases, this file is automatically
# released to the Public Domain.
#

pkgbase=trinity
pkgname=trinity-kdebase
pkgver=1240522
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.trinitydesktop.org'
license=('GPL')
groups=('trinity')
pkgdesc="Trinity - kdebase"
depends=('hal'
        'trinity-dbus-1-qt3'
        'trinity-kdelibs')
makedepends=('pkgconfig'
        'cmake'
        'autoconf'
        'imake'
        'subversion'
        'xorg-bdftopcf')
optdepends=('desktop-file-utils: Command line utilities for working with desktop entries'
        'pciutils: PCI bus configuration space access library and tools'
        'xscreensaver: Screen saver and locker for the X Window System')
provides=('trinity-kdebase')
conflicts=('trinity-kdebase')
replaces=('trinity-kdebase')
options=('libtool' '!strip')
source=()

_svnmod=kdebase
_svntrunk="svn://anonsvn.kde.org/home/kde/branches/trinity/${_svnmod}"

_qtdir=/opt/qt
_prefix="/opt/trinity"

_tdesktop() {
    # create/install desktop
    install -d -m755 ${pkgdir}/etc/X11/sessions
    echo '[Desktop Entry]' > ${pkgdir}/etc/X11/sessions/trinity.desktop
    echo 'Encoding=UTF-8' >> ${pkgdir}/etc/X11/sessions/trinity.desktop
    echo 'Type=XSession' >> ${pkgdir}/etc/X11/sessions/trinity.desktop
    echo "Exec=${_prefix}/bin/startkde" >> ${pkgdir}/etc/X11/sessions/trinity.desktop
    echo "TryExec=${_prefix}/bin/startkde" >> ${pkgdir}/etc/X11/sessions/trinity.desktop
    echo 'Name=Trinity' >> ${pkgdir}/etc/X11/sessions/trinity.desktop
    echo 'Comment=The Trinity Desktop Environment. A powerful Open Source graphical desktop environment' >> ${pkgdir}/etc/X11/sessions/trinity.desktop
    chmod 0755 ${pkgdir}/etc/X11/sessions/trinity.desktop
}

build() {

  cd ${srcdir}

  msg "Connecting to SVN server to update or checkout ${_svnmod}...."
  if [ -d ${_svnmod}/.svn ]; then
    (cd ${_svnmod} && svn up)
    [[ $? -eq 0 ]] || _co_failed=1
  else
    (svn co $_svntrunk ${_svnmod})
    [[ $? -eq 0 ]] || _co_failed=1
  fi

  # if update or checkout failed - bail...
  if [[ $_co_failed -ne 1 ]]; then
    msg "SVN checkout of revision $pkgver -- Complete."
  else
    msg "SVN checkout of revision $pkgver -- Failed or server timeout."
    exit 1
  fi

  msg "Setting PATH, CMAKE and Trinity Environment variables"
  if [[ -r /etc/profile.d/qt3.sh ]]; then
    . /etc/profile.d/qt3.sh
    [[ -n $QTDIR ]] && _qtdir=$QTDIR
  else
    [[ ${PATH%%:*} =~ ${_qtdir}/bin ]] || export PATH=${_qtdir}/bin:$PATH
  fi
  export CMAKE_PREFIX_PATH=/opt/qt:/opt/trinity
  export CMAKE_INCLUDE_PATH=/opt/qt/include/tqt:/opt/trinity/include/dbus-1.0:/opt/trinity/include/dbus-1.0/dbus:/opt/trinity/include:/opt/trinity/include/libkrandr
  export LD_LIBRARY_PATH=/opt/trinity/lib:/opt/trinity/lib/kde3:$LD_LIBRARY_PATH
  export PKG_CONFIG_PATH=:/opt/trinity/lib/pkgconfig:/opt/qt/lib/pkgconfig

#   cd ${srcdir}/${_svnmod}

  cd $srcdir
  msg "Creating out-of-source build directory: ${srcdir}/build"
  mkdir -p build
  cd build

  msg "Starting cmake..."
  cmake ${srcdir}/${_svnmod} \
    -DCMAKE_INSTALL_PREFIX=${_prefix} \
    -DCMAKE_VERBOSE_MAKEFILE=ON \
    -DQT_VERSION=3 \
        -DSYSCONF_INSTALL_DIR=/etc \
        -DWITH_XFIXES=ON \
        -DWITH_HAL=ON \
        -DWITH_ARTS=ON \
        -DWITH_SASL=ON \
        -DWITH_OPENEXR=ON \
        -DWITH_XCOMPOSITE=ON \
        -DWITH_XCURSOR=ON \
        -DWITH_XRANDR=ON \
        -DWITH_XRENDER=ON \
        -DWITH_XDAMAGE=ON \
        -DWITH_SHADOW=ON \
        -DWITH_XDMCP=ON \
        -DWITH_XINERAMA=ON \
        -DWITH_XEXT=ON \
    -DWITH_PAM=ON \
    -DBUILD_ALL=ON \
    -DCMAKE_INCLUDE_DIR=/opt/trinity/include/dbus-1.0:/opt/qt/include/tqt:/opt/trinity/include/dbus-1.0/dbus
  
  make
#     -DWITH_QT3=ON \
#     -DQTDIR=/opt/qt \
#     -DQT_LIBRARY_DIRS=/opt/qt/lib \
}

package() {
  msg "Packaging - $pkgname-$pkgver"
#   cd ${srcdir}/${_svnmod}
  cd ${srcdir}/build

  make DESTDIR="$pkgdir/" install

  cd ${startdir}

  # install desktop and update kdmrc and Xsession
#   [[ -f trinity.desktop ]] && {
#     install -d -m755 ${pkgdir}/etc/X11/sessions
#     install -m644 trinity.desktop ${pkgdir}/etc/X11/sessions
#   }
  _tdesktop
  [[ -f ${pkgdir}/etc/X11/sessions/trinity.desktop ]] || msg "ERROR - Install Failure: ${pkgdir}/etc/X11/sessions/trinity.desktop"

  # set kdm directory
  _kdmdir=${_prefix}/share/config/kdm
  [[ -d ${pkgdir}/${_kdmdir} ]] || install -d -m755 ${pkgdir}/${_kdmdir}

  # install kdmrc
  [[ -f kdmrc ]] && install -m644 kdmrc ${pkgdir}/${_kdmdir}

  # update the Xsession file
  [[ -f Xsession ]] && install -m755 Xsession ${pkgdir}/${_kdmdir}

  # create the starttrinity link
  [[ -d ${pkgdir}/usr/bin ]] || mkdir -p ${pkgdir}/usr/bin
  cd ${pkgdir}/usr/bin
  ln -sf /opt/trinity/bin/startkde starttrinity

  cd ${startdir}

#   rm -r ${srcdir}/${_svnmod}
}
