# Maintainer: Pawel 'l0ner' Soltys <pwslts@gmail.com>
# Contributor: Calvin Morrison <mutantturkey@gmail.com>

pkgname=trinity-arts
pkgver=3513
pkgrel=3
arch=('i686' 'x86_64')
url='http://www.trinitydesktop.com'
license=('GPL')
groups=('trinity-base')
pkgdesc="Trinity ARTS Sound System and libraries"
depends=('trinity-tqtinterface'
         'audiofile' 'libmad' 'libvorbis'
         'glib2')
makedepends=('pkgconfig' 'cmake')
conflicts=('kdemod3-arts' 'arts')
options=('libtool' '!emptydirs')
source=('http://mirror.ets.kth.se/trinity/releases/3.5.13/dependencies/arts-3.5.13.tar.gz'
        'trinity.profile.sh' 'trinity.profile.csh')
md5sums=('109124dabb1aaa9d44eb32e43b549142'
         '72c6c4fb492c1f4b4741fad1dd70d62f'
         '915e14c6264a6b9c46a6a20b16cdcd06')
install='trinity-arts.install'

_prefix="/opt/trinity"

build() {

   # Do we need this?
   msg "Setting PATH, CMAKE and Trinity Environment variables"
   # Source the QT and KDE profile
   [ "$QTDIR" = "" ] && . /etc/profile.d/qt3.sh 

   cd $srcdir
   msg "Creating out-of-source build directory: ${srcdir}/build"
   mkdir -p build
   cd build

   msg "Starting cmake..."
   cmake ${srcdir}/dependencies/arts/ \
      -DCMAKE_INSTALL_PREFIX=${_prefix}

   msg "Building $pkgname..."
   make

}

package() {
   msg "Packaging - $pkgname-$pkgver"

   cd ${srcdir}/build

   make DESTDIR="$pkgdir" install

   # since this is first package to provide
   # binaries, include headers and libs from trinity
   # it should contain sripts which would add
   # their location into system variables
   # this mean that this package should provide
   # /etc/ld.so.conf.d/trinity.conf - lib locations for linker
   # /etc/profile.d/trinity.sh - $PATH and other system variables for bash
   # /etc/profile.d/trinity.csh - $PATH and other system variables for csh
   # up until now it was done in the kdelibs PKGBUILD/package

   # Create and install ld.so.conf.d file
   install -d -m755 $pkgdir/etc/ld.so.conf.d/
   echo $_prefix/lib > $pkgdir/etc/ld.so.conf.d/trinity.conf
   echo $_prefix/lib/kde >> $pkgdir/etc/ld.so.conf.d/trinity.conf

   # Install profiles
   install -D -m644 $srcdir/trinity.profile.sh $pkgdir/etc/profile.d/trinity.sh
   install -D -m644 $srcdir/trinity.profile.csh $pkgdir/etc/profile.d/trinity.csh

}
