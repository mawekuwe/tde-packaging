# Maintainer: Pawel 'l0ner' Soltys <pwslts@gmail.com>
# Contributor: Calvin Morrison <mutantturkey@gmail.com>

pkgname=trinity-kdelibs
pkgver=3513
pkgrel=5
arch=('i686' 'x86_64')
url='http://www.trinitydesktop.org'
license=('GPL')
groups=('trinity-base')
pkgdesc="Trinity DE core libraries"
depends=('trinity-arts'
         'libxslt' 'libidn' 'libart-lgpl' 'jasper' 
         'libxcomposite' 'libcups' 'libutempter'
         'openexr' 'aspell' 'hspell') 
         # taglib openssl libtiff avahi libltdl pam-krb5 libical
         # libutempter - needs to be installed from aur
makedepends=('pkgconfig' 'cmake')
provides=('kdelibs3')
conflicts=('kdemod3-kdelibs' 'kdelibs3')
replaces=('trinity-kdelibs')
options=('libtool' '!makeflags')
source=('http://mirror.ets.kth.se/trinity/releases/3.5.13/kdelibs-3.5.13.tar.gz' 'docpath.patch')
md5sums=('f62eefb63b4ba2141b4c576f859806dd'
         'd20badec5a1a7f01f0c957db8b9b8def')

install='trinity-kdelibs.install'

build() {
   msg "Setting PATH, CMAKE and Trinity Environment variables"
   # Source the QT and TDE profile
   [ "$QTDIR" = "" ] && . /etc/profile.d/qt3.sh 
   [ "$TDEDIR" = "" ] && . /etc/profile.d/trinity.sh

   # compiles without exporting this, since we have profiles
   #export CMAKE_PREFIX_PATH=${QTDIR}:${TDEDIR}
   #export PATH=$PATH:${TDEDIR}/bin
   #export CMAKE_INCLUDE_PATH=/usr/include/dbus-1.0
   export LD_LIBRARY_PATH=${TDEDIR}/lib:${QTDIR}/lib:${TDEDIR}/lib/trinity:$LD_LIBRARY_PATH

   msg "Patching..."
   patch -N -d ${srcdir} -p0 < ${srcdir}/docpath.patch

   cd $srcdir
   msg "Creating out-of-source build directory: ${srcdir}/${_builddir}"
   mkdir -p build
   cd build

   msg "Starting cmake..."
   cmake ${srcdir}/kdelibs \
    -DCMAKE_INSTALL_PREFIX=${TDEDIR} \
    -DCMAKE_VERBOSE_MAKEFILE=OFF \
    -DWITH_ARTS=ON \
    -DWITH_ALSA=ON \
    -DWITH_LIBART=ON \
    -DWITH_LIBIDN=ON \
    -DWITH_SSL=ON \
    -DWITH_CUPS=ON \
    -DWITH_LUA=OFF \
    -DWITH_TIFF=ON \
    -DWITH_JASPER=ON \
    -DWITH_OPENEXR=ON \
    -DWITH_UTEMPTER=ON \
    -DWITH_AVAHI=ON \
    -DWITH_ASPELL=ON \
    -DWITH_HSPELL=ON

   msg "Building - $pkgname..."
   make
}

package() {
   msg "Packaging - $pkgname-$pkgver"
   cd ${srcdir}/build

   make DESTDIR="$pkgdir" install
}
