# Maintainer: Calvin Morrison <mutantturkey@gmail.com>
# Contributor: Pawel 'l0ner' Soltys <pwslts@gmail.com>

pkgname=trinity-kdelibs
pkgver=3513
pkgrel=4
arch=('i686' 'x86_64')
url='http://www.trinitydesktop.org'
license=('GPL')
groups=('trinity-base')
pkgdesc="Trinity DE core libraries"
depends=('trinity-arts' 'trinity-qt3' 'trinity-tqtinterface' 'libical' 'libxslt' 'libltdl' 'avahi' 'pam-krb5' 'taglib' 'libxcomposite' 'libcups' 'alsa-lib' 'libart-lgpl' 'libidn' 'openssl' 'libtiff' 'jasper' 'openexr' 'aspell' 'hspell') 
makedepends=('pkgconfig' 'cmake')
provides=('trinity-kdelibs' 'kdelibs3')
conflicts=('trinity-kdelibs' 'kdelibs3')
replaces=('trinity-kdelibs')
options=('libtool' '!strip' '!makeflags')
source=('http://mirror.ets.kth.se/trinity/releases/3.5.13/kdelibs-3.5.13.tar.gz' 'trinity.sh' 'docpath.patch')
md5sums=('f62eefb63b4ba2141b4c576f859806dd'
         '2de1886d632c8bc288befada5e1bc3b5'
         'd20badec5a1a7f01f0c957db8b9b8def')

_builddir=build
_prefix="/opt/trinity"

build() {
   msg "Setting PATH, CMAKE and Trinity Environment variables"
   #if [[ -r /etc/profile.d/qt3.sh ]]; then
    . /etc/profile.d/qt3.sh
   # [[ -n $QTDIR ]] && _qtdir=$QTDIR
   #else
   # [[ ${PATH%%:*} =~ ${_qtdir}/bin ]] || export PATH=${_qtdir}/bin:$PATH
   #fi
   export CMAKE_PREFIX_PATH=${QTDIR}:${_prefix}
   export PATH=$PATH:${_prefix}/bin
   export CMAKE_INCLUDE_PATH=${QTDIR}/include/tqt:/usr/include/dbus-1.0:${_prefix}/bin
   export LD_LIBRARY_PATH=${_prefix}/lib:${QTDIR}/lib:${_prefix}/lib/trinity:$LD_LIBRARY_PATH

   msg "Patching..."
   patch -N -d ${srcdir} -p0 < ${srcdir}/docpath.patch

   cd $srcdir
   msg "Creating out-of-source build directory: ${srcdir}/${_builddir}"
   mkdir -p ${_builddir}
   cd ${_builddir}

   msg "Starting cmake..."
   cmake ${srcdir}/kdelibs \
    -DCMAKE_INSTALL_PREFIX=${_prefix} \
    -DCMAKE_VERBOSE_MAKEFILE=OFF \
    -DWITH_ARTS=ON \
    -DWITH_ALSA=ON \
    -DWITH_LIBART=ON \
    -DWITH_LIBIDN=ON \
    -DWITH_SSL=ON \
    -DWITH_CUPS=ON \
    -DWITH_LUA=OFF \
    -DWITH_TIFF=ON \
    -DWITH_JASPER=ON \
    -DWITH_OPENEXR=ON \
    -DWITH_UTEMPTER=OFF \
    -DWITH_AVAHI=ON \
    -DWITH_ASPELL=ON \
    -DWITH_HSPELL=ON

# Not needed, CMake returns that those were not used
#    -DQT_VERSION=3 \
#    -DWITH_QT3=ON \
#    -DQTDIR=${QTDIR} \
#    -DQT_LIBRARY_DIRS=${QTDIR}/lib \
#    -DWITH_PAM=ON \
#    -DBUILD_ALL=ON

   msg "Building - $pkgname..."
   make
}

package() {
   msg "Packaging - $pkgname-$pkgver"
   cd ${srcdir}/build

   make DESTDIR="$pkgdir" install

   install -d -m755 ${pkgdir}/etc/ld.so.conf.d/
   echo "${_prefix}/lib" > ${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf

   install -d -m755 ${pkgdir}/etc/profile.d/
   install -m644 ${srcdir}/trinity.sh ${pkgdir}/etc/profile.d/

   #   rm -r ${srcdir}/${_svnmod}
}
