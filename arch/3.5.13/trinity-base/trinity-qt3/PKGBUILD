# Maintainer: Calvin Morrison <MutantTurkey@gmail.com>

pkgname=trinity-qt3
pkgver=3884
pkgrel=2
arch=('i686' 'x86_64')
url="http://trinitydesktop.org"
license=('GPL')
groups=('trinity-base')
pkgdesc="The Qt3 gui toolkit - with Trinity upstream"
depends=('libjpeg-turbo' 'libmng' 'libmysqlclient' 'libpng' 'libxmu'  'libxcursor' 'libxinerama' 'libxft' 'libxrandr' 'mesa' 'postgresql-libs' 'unixodbc')
makedepends=('cups' 'libxi' 'mysql' 'postgresql' 'unixodbc' 'sqlite3')
# optdepends=('libmysqlclient' 'postgresql-libs' 'unixodbc')
provides=('trinity-qt3' 'qt3')
conflicts=('qt3' 'qt3-enhanced')
replaces=('trinity-qt3')
options=(!libtool)
source=(http://mirror.ets.kth.se/trinity/releases/3.5.13/dependencies/qt3-3.3.8.d.tar.gz)
md5sums=('78dc675e84aed595375449818cbb589a')

_prefix="/opt/qt"
_builddir="qt3"
# install=qt.install

build() {
   msg "Setting enviroment variables..."
   export QTDIR="${srcdir}/${_builddir}/" 
   export PATH=${QTDIR}/bin:${PATH}
   export LD_LIBRARY_PATH=${QTDIR}/lib:${LD_LIBRARY_PATH}
   export QMAKESPEC=$QTDIR/mkspecs/linux-g++

   if [ "$CARCH" = "x86_64" ]; then
     export ARCH="-64"
     else unset ARCH
   fi

   cd "${srcdir}/${_builddir}"

   rm -rf mkspecs/{*aix*,*bsd*,cygwin*,dgux*,darwin*,hpux*,hurd*,irix*,lynxos*,macx*,qnx*,reliant*,sco*,solaris*,tru64*,unixware*,win32*}

   msg "Starting configure..."
   #make -C qmake || return 1
   ./configure \
      -prefix ${_prefix} \
      -fast \
      -sysconfdir /etc/qt \
      -thread \
      -shared \
      -system-zlib \
      -platform linux-g++$ARCH \
      -system-lib{png,jpeg,mng} \
      -qt-gif \
      -cups

   msg "Building - $pkgname..."
   make  
}

package() {  
   msg "Packaging - $pkgname-$pkgver"
   cd "$srcdir/${_builddir}"

   # Build ld.so.conf file
   echo "${_prefix}" > ${srcdir}/libqt-mt.conf
   echo "${_prefix}/lib" >> ${srcdir}/libqt-mt.conf
   echo "/opt/trinity/lib" >> ${srcdir}/libqt-mt.conf
   # install -d -m755 ${pkgdir}/etc/ld.so.conf.d/
   install -D -m755 ${srcdir}/libqt-mt.conf  ${pkgdir}/etc/ld.so.conf.d/libqt-mt.conf

   # Build and install qt.profile
   echo "export QTDIR=${_prefix}" > ${srcdir}/qt.profile
   echo "export QT_XFT=true" >> ${srcdir}/qt.profile
   echo 'export PATH=$PATH:$QTDIR/bin' >> ${srcdir}/qt.profile
   echo 'export PKG_CONFIG_PATH=$PKG_CONFIG_PATH:$QTDIR/pkgconfig' >> ${srcdir}/qt.profile
   install -D -m755 ${srcdir}/qt.profile ${pkgdir}/etc/profile.d/qt3.sh

   make INSTALL_ROOT="$pkgdir" install
} 
