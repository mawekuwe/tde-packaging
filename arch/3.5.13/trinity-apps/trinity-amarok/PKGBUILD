# Maintainer: Pawel 'l0ner' Soltys <pwslts@gmail.com>
# Contributor: Calvin Morrison <mutantturkey@gmail.com>

pkgname=trinity-amarok
pkgver=3513
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.trinitydesktop.org'
license=('GPL')
groups=('trinity-apps')
pkgdesc="Trinity Amarok music player and manager"
depends=('trinity-kdebase' 'libifp' 'libmtp' 'taglib' 'libnjb' 'python-daap' 'ruby')
makedepends=('pkgconfig' 'cmake' 'imake')
options=('libtool' '!strip')
source=(http://mirror.ets.kth.se/trinity/releases/3.5.13/applications/amarok-3.5.13.tar.gz)
md5sums=('7145ebee38f104bef6cfaf56e7f81959')


build() {

  msg "Setting PATH, CMAKE and Trinity Environment variables"
  export CMAKE_PREFIX_PATH=/opt/qt:/opt/trinity
  export CMAKE_INCLUDE_PATH=/opt/qt/include/tqt:/usr/include/dbus-1.0:/opt/trinity/include
  export LD_LIBRARY_PATH=/opt/trinity/lib:/opt/trinity/lib/kde3:$LD_LIBRARY_PATH
  export PKG_CONFIG_PATH=:/opt/qt/lib/pkgconfig

  _prefix="/opt/trinity"

  cd $srcdir
  msg "Creating out-of-source build directory: ${srcdir}/build"
  mkdir -p build
  cd build

  # Baho additions
  CFLAGS=${CFLAGS}" -fpermissive"
  CXXFLAGS=${CXXFLAGS}" -fpermissive"

  msg "Starting cmake..."
  cmake ${srcdir}/applications/amarok \
    -DCMAKE_INSTALL_PREFIX=${_prefix} \
    -DCMAKE_VERBOSE_MAKEFILE=ON \
    -DQT_VERSION=3 \
    -DWITH_IFP=ON \
    -DWITH_NJB=ON \
    -DWITH_MTP=ON \
    -DWITH_DAAP=ON \
    -DBUILD_ALL=ON
  make
}

package() {
  msg "Packaging - $pkgname-$pkgver"
  cd ${srcdir}/build

  make DESTDIR="$pkgdir/" install

#   rm -r ${srcdir}/${_svnmod}
}
