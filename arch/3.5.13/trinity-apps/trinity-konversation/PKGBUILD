# Maintainer: Calvin Morrison <mutantturkey@gmail.com>

pkgname=trinity-konversation
pkgver=3513
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.trinitydesktop.org'
license=('GPL')
groups=('trinity-apps')
pkgdesc="Trinity Konversation IRC client"
depends=('trinity-kdelibs')
makedepends=('pkgconfig' 'autoconf' 'automake')
options=('libtool' '!strip')
source=(http://mirror.ets.kth.se/trinity/releases/3.5.13/applications/konversation-3.5.13.tar.gz)
md5sums=('0f0e0415dad4c19bc885c9addadf4026')

_builddir=applications/konversation
_prefix=/opt/trinity

build() {
  msg "Setting Trinity Environment variables"
  export LD_LIBRARY_PATH=${_prefix}/lib:${_prefix}/lib/trinity:$LD_LIBRARY_PATH
  export PKG_CONFIG_PATH=${_prefix}/lib/pkgconfig:${QTDIR}/lib/pkgconfig
  
  msg "Starting autoconf..."
  cd "$srcdir/$_builddir"
  cp -f "/usr/share/aclocal/libtool.m4" "admin/libtool.m4.in"
  cp -f "/usr/share/libtool/config/ltmain.sh" "admin/ltmain.sh"

  # fix for correct Qt/TQt detection
  sed -i -e "s@/usr/include/tqt@${TDEDIR}/include/tqt@" admin/acinclude.m4.in

  make -f admin/Makefile.common

  msg "Starting configure..."
  ./configure \
     --with-qt-dir=${QTDIR} \
     --with-qt-inculdes=${QTDIR}/include \
     --with-qt-libraries=${QTDIR}/lib \
     --prefix=${_prefix} \
     --with-extra-includes=${_prefix}/include:/usr/include/dbus-1.0:${_prefix}/include/libkrandr \
     --with-extra-libs=${_prefix}/lib:${_prefix}/lib/trinity:${_prefix}/pkgconfig

  msg "Building ${pkgname}..."
  make
}

package() {
  msg "Packaging - $pkgname-$pkgver"
#  cd ${srcdir}/build
  cd ${srcdir}/${_builddir}
  make DESTDIR="${pkgdir}" install
}
