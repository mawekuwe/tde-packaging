# Maintainer: Pawel 'l0ner' Soltys <pwslts@gmail.com>
# Contributor: Calvin Morrison <mutantturkey@gmail.com>

pkgname=trinity-kdevelop
pkgver=3513
pkgrel=2
arch=('i686' 'x86_64')
url='http://www.trinitydesktop.org'
license=('GPL')
groups=('trinity-extras')
pkgdesc="Trinity Developement tools"
depends=('trinity-kdebase')
makedepends=('pkgconfig' 'cmake' 'imake')
options=('libtool' '!strip')
source=('http://mirror.ets.kth.se/trinity/releases/3.5.13/kdevelop-3.5.13.tar.gz' 'kdevelop.patch')
md5sums=('777bd77e4f2dc6a5b5b45a370dcf6da0'
         'ab23854b1658adc1da4621cb589ec1be')

build() {
  msg "Setting PATH, CMAKE and Trinity Environment variables"
  [ "$QTDIR" = "" ] && . /etc/profile.d/qt3.sh
  [ "$TDEDIR" = "" ] && . /etc/profile.d/trinity.sh

  msg "Patching..."
  # apply second part of Francois Andriot patch
  patch -p0 -i ${srcdir}/kdevelop.patch

  cd $srcdir
  msg "Creating out-of-source build directory: ${srcdir}/build"
  mkdir -p build
  cd build

  msg "Starting cmake..."
  cmake ${srcdir}/kdevelop \
    -DCMAKE_INSTALL_PREFIX=${TDEDIR} \
    -DCMAKE_VERBOSE_MAKEFILE=ON \
    -DQT_VERSION=3 \
    -DWITH_QT3=ON \
    -DQTDIR=/opt/qt \
    -DQT_DOCDIR=/opt/qt/man \
    -DQT_LIBRARY_DIRS=/opt/qt/lib \
    -DBUILD_ALL=ON
  make
}

package() {
  msg "Packaging - $pkgname-$pkgver"
  cd ${srcdir}/build
  make DESTDIR="$pkgdir/" install
}
