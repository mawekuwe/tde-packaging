# Maintainer: Pawel 'l0ner' Soltys <pwslts@gmail.com>
# Contributor: Calvin Morrison <mutantturkey@gmail.com>

pkgname=trinity-kdemultimedia
pkgver=3513
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.trinitydesktop.org'
license=('GPL')
groups=('trinity-extras')
pkgdesc="Trinity multimedia applications and tools"
depends=('trinity-kdelibs' 'xine-lib' 'libvorbis' 'trinity-arts' 'alsa-lib' 'cdparanoia' 'lame' 'audiofile' 'libtheora' 'flac')
makedepends=('pkgconfig' 'autoconf' 'automake')
options=('libtool' '!strip')
source=('http://mirror.ets.kth.se/trinity/releases/3.5.13/kdemultimedia-3.5.13.tar.gz' 'xine_artsplugin.patch')
md5sums=('6d906b8a8fd0928e01ecd600175381c0' 'bb045d94e8dca80d73af4add43b7fc15')

_prefix=/opt/trinity

build() {

  msg "Setting Trinity Environment variables"
  export LD_LIBRARY_PATH=${_prefix}/lib:${_prefix}/lib/trinity:$LD_LIBRARY_PATH
  export PKG_CONFIG_PATH=${_prefix}/lib/pkgconfig:${QTDIR}/lib/pkgconfig

  msg "Patching..."
  patch -p0 -i ${srcdir}/xine_artsplugin.patch
  
  msg "Starting automake..."
  cd "$srcdir/kdemultimedia"
  cp -f "/usr/share/aclocal/libtool.m4" "admin/libtool.m4.in"
  cp -f "/usr/share/libtool/config/ltmain.sh" "admin/ltmain.sh"
  make -f admin/Makefile.common

  msg "Starting configure..."
  ./configure --with-qt-dir=${QTDIR} \
     --prefix=${_prefix} \
     --with-extra-includes=${_prefix}/include:/usr/include/dbus-1.0:${_prefix}/include/libkrandr \
     --with-extra-libs=${_prefix}/lib:${_prefix}/lib/trinity:${_prefix}/pkgconfig \
     --enable-closure \
     --with-vorbis \
     --with-alsa \
     --with-cdparanoia \
     --with-lame \
     --with-audiofile \
     --with-theora \
     --with-flac \
     --with-kscd-cdda \
     --with-xine
     #--with-gstreamer
     #--with-akode \

  msg "Building ${pkgname}..."
  make
}

package() {
  msg "Packaging - $pkgname-$pkgver"
  cd ${srcdir}/kdemultimedia
  make DESTDIR="${pkgdir}" install
}
