# Maintainer: Pawel 'l0ner' Soltys <pwslts@gmail.com>
# Contributor: Calvin Morrison <mutantturkey@gmail.com>

pkgname=trinity-kdeaddons
pkgver=3513
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.trinitydesktop.org'
license=('GPL')
groups=('trinity-extras')
pkgdesc="Trinity DE addons collection"
depends=('trinity-kdelibs' 'trinity-kdebase' 'xmms')
makedepends=('pkgconfig' 'autoconf' 'automake')
optdepends=()
provides=($pkgname)
conflicts=()
replaces=()
options=('libtool' '!strip')
source=(http://mirror.ets.kth.se/trinity/releases/3.5.13/kdeaddons-3.5.13.tar.gz)
md5sums=('f5fa96a5d99d1bc33600f609881eaee6')

build() {
  msg "Setting Trinity Environment variables"
  [ "$QTDIR" = "" ] && . /etc/profile.d/qt3.sh
  [ "$TDEDIR" = "" ] && . /etc/profile.d/trinity.sh
  
  msg "Starting automake..."
  cd "$srcdir/kdeaddons"
  cp -f "/usr/share/aclocal/libtool.m4" "admin/libtool.m4.in"
  cp -f "/usr/share/libtool/config/ltmain.sh" "admin/ltmain.sh"
  sed -i -e "s@/usr/include/tqt@$TDEDIR/include/tqt@" admin/acinclude.m4.in
  make -f admin/Makefile.common

  msg "Starting configure..."
  ./configure --with-qt-dir=${QTDIR} \
     --prefix=${TDEDIR} \
     --enable-closure \
     --with-{sdl,xmms} \
     #--with-extra-includes=${TDEDIR}/include:/usr/include/dbus-1.0:${TDEDIR}/include/libkrandr \
     #--with-extra-libs=${TDEDIR}/lib:${TDEDIR}/lib/trinity:${TDEDIR}/pkgconfig

  msg "Building ${pkgname}..."
  make
}

package() {
  msg "Packaging - $pkgname-$pkgver"
  cd ${srcdir}/kdeaddons
  make DESTDIR="${pkgdir}" install
}
