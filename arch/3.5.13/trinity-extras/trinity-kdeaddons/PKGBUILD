# Maintainer: Calvin Morrison <mutantturkey@gmail.com>

pkgname=trinity-kdeaddons
pkgver=3513
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.trinitydesktop.org'
license=('GPL')
groups=('trinity-extras')
pkgdesc="Trinity DE addons collection"
depends=('trinity-kdelibs' 'trinity-kdebase' 'xmms')
makedepends=('pkgconfig' 'autoconf' 'automake')
optdepends=()
provides=($pkgname)
conflicts=()
replaces=()
options=('libtool' '!strip')
source=(http://mirror.its.uidaho.edu/pub/trinity/releases/3.5.13/kdeaddons-3.5.13.tar.gz)
md5sums=('f5fa96a5d99d1bc33600f609881eaee6')

_builddir=kdeaddons
_prefix=/opt/trinity

build() {

  msg "Setting Trinity Environment variables"
  export LD_LIBRARY_PATH=${_prefix}/lib:${_prefix}/lib/trinity:$LD_LIBRARY_PATH
  export PKG_CONFIG_PATH=${_prefix}/lib/pkgconfig:${QTDIR}/lib/pkgconfig

  #msg "Patching..."
  #patch -N -d ${srcdir} -p0 < kdeaddons.patch
  
  msg "Starting automake..."
  cd "$srcdir/${_builddir}"
  cp -f "/usr/share/aclocal/libtool.m4" "admin/libtool.m4.in"
  cp -f "/usr/share/libtool/config/ltmain.sh" "admin/ltmain.sh"
  make -f admin/Makefile.common

  msg "Starting configure..."
  ./configure --with-qt-dir=${QTDIR} \
     --prefix=${_prefix} \
     --enable-closure \
     --with-{sdl,xmms} \
     --with-extra-includes=${_prefix}/include:/usr/include/dbus-1.0:${_prefix}/include/libkrandr \
     --with-extra-libs=${_prefix}/lib:${_prefix}/lib/trinity:${_prefix}/pkgconfig

  msg "Building ${pkgname}..."
  make
}

package() {
  msg "Packaging - $pkgname-$pkgver"
  cd ${srcdir}/${_builddir}
  make DESTDIR="${pkgdir}" install
}
