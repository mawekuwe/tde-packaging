# Maintainer: Pawel 'l0ner' Soltys <pwslts@gmail.com>
# Contributor: Calvin Morrison <mutantturkey@gmail.com>

pkgname=trinity-kdenetwork
pkgver=3513
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.trinitydesktop.org'
license=('GPL')
groups=('trinity-extras')
pkgdesc="Trinity network utilities"
depends=('trinity-kdebase' 
         'trinity-arts' 
         'speex' 'wireless_tools'
         'gsmlib-git'
         'libgadu'
         'meanwhile'
         'sqlite3'
         'libidn'
         'v4l-utils')
makedepends=('pkgconfig' 'cmake' 'imake')
options=('libtool' '!strip')
source=('http://mirror.ets.kth.se/trinity/releases/3.5.13/kdenetwork-3.5.13.tar.gz' 'kdenetwork.patch')
md5sums=('349f197c9af4bf0d9b8773f26829415e'
         'ee468902b7c40ac1cc86cbaababa35dc')

build() {
  msg "Setting PATH, CMAKE and Trinity Environment variables"
  export CMAKE_PREFIX_PATH=/opt/qt:/opt/trinity
  export CMAKE_INCLUDE_PATH=/opt/qt/include/tqt:/usr/include/dbus-1.0:/opt/trinity/include
  export LD_LIBRARY_PATH=/opt/trinity/lib:/opt/trinity/lib/kde3:$LD_LIBRARY_PATH
  export PKG_CONFIG_PATH=:/opt/qt/lib/pkgconfig

  _prefix="/opt/trinity"

  patch -p0 -i ${srcdir}/kdenetwork.patch
  mv ${srcdir}/kdenetwork/kopete/protocols/jabber/jingle/libjingle/talk/xmllite/qname.cc ${srcdir}/kdenetwork/kopete/protocols/jabber/jingle/libjingle/talk/xmllite/tqname.cc
  mv ${srcdir}/kdenetwork/kopete/protocols/jabber/jingle/libjingle/talk/xmllite/qname.h ${srcdir}/kdenetwork/kopete/protocols/jabber/jingle/libjingle/talk/xmllite/tqname.h

  cd $srcdir
  msg "Creating out-of-source build directory: ${srcdir}/build"
  mkdir -p build
  cd build

  msg "Starting cmake..."
  cmake ${srcdir}/kdenetwork \
    -DCMAKE_INSTALL_PREFIX=${_prefix} \
    -DCMAKE_VERBOSE_MAKEFILE=ON \
    -DWITH_JINGLE=ON \
    -DWITH_SPEEX=ON \
    -DWITH_WEBCAM=ON \
    -DWITH_GSM=ON \
    -DWITH_ARTS=ON \
    -DBUILD_KOPETE_PROTOCOL_ALL=ON \
    -DBUILD_KOPETE_PLUGIN_ALL=ON \
    -DBUILD_ALL=ON
  make
}

package() {
  msg "Packaging - $pkgname-$pkgver"
  cd ${srcdir}/build

  make DESTDIR="$pkgdir/" install
}
