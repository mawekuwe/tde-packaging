# Maintainer: Pawel 'l0ner' Soltys <pwslts@gmail.com>
# Contributor: Calvin Morrison <mutantturkey@gmail.com>

pkgname=trinity-webdev
pkgver=3513
pkgrel=2
arch=('i686' 'x86_64')
url='http://www.trinitydesktop.org'
license=('GPL')
groups=('trinity-extras')
pkgdesc="Trinity Web development tools"
depends=('trinity-kdebase')
makedepends=('pkgconfig' 'cmake' 'imake')
options=('libtool' '!strip')
source=('http://mirror.ets.kth.se/trinity/releases/3.5.13/kdewebdev-3.5.13.tar.gz' 'kdewebdev.patch')
md5sums=('4f56a6c4b5916615ca3615f8e8f868c6'
         '5ef6636db45e35d6dcda35332bdca931')

build() {
  msg "Setting PATH, CMAKE and Trinity Environment variables"
  [ "$QTDIR" = "" ] && . /etc/profile.d/qt3.sh
  [ "$TDEDIR" = "" ] && . /etc/profile.d/trinity.sh
  patch -N -d ${srcdir} -p0 < kdewebdev.patch

  cd $srcdir
  msg "Creating out-of-source build directory: ${srcdir}/build"
  mkdir -p build
  cd build

  msg "Starting cmake..."
  cmake ${srcdir}/kdewebdev \
    -DCMAKE_INSTALL_PREFIX=${TDEDIR} \
    -DCMAKE_VERBOSE_MAKEFILE=ON \
    -DBUILD_ALL=ON
  make
}

package() {
  msg "Packaging - $pkgname-$pkgver"
  cd ${srcdir}/build
  make DESTDIR="$pkgdir" install
}
