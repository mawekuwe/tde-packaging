# Maintainer: Calvin Morrison <mutantturkey@gmail.com>

pkgname=trinity-kdesdk
pkgver=3513
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.trinitydesktop.org'
license=('GPL')
groups=('trinity-extras')
pkgdesc="Trinity Software Developement Kit"
depends=('trinity-kdebase')
makedepends=('pkgconfig' 'cmake' 'imake')
options=('libtool' '!strip')
#source=('http://mirror.its.uidaho.edu/pub/trinity/releases/3.5.13/kdesdk-3.5.13.tar.gz' 'kdesdk.patch')
source=('http://anduin.linuxfromscratch.org/sources/trinity/kdesdk-3.5.13.tar.gz' 'kdesdk.patch')
md5sums=('130662f683be1a49d56a713c5ae87e46'
         'a1d0b533ae427847bd84e601c587f882')

_svnmod=kdesdk

build() {
  msg "Setting PATH, CMAKE and Trinity Environment variables"
  export CMAKE_PREFIX_PATH=/opt/qt:/opt/trinity
  export CMAKE_INCLUDE_PATH=/opt/qt/include/tqt:/usr/include/dbus-1.0:/opt/trinity/include
  export LD_LIBRARY_PATH=/opt/trinity/lib:/opt/trinity/lib/kde3:$LD_LIBRARY_PATH
  export PKG_CONFIG_PATH=:/opt/qt/lib/pkgconfig

  trinity_prefix="/opt/trinity"

  patch -N -d ${srcdir} -p0 < kdesdk.patch

  cd $srcdir
  msg "Creating out-of-source build directory: ${srcdir}/build"
  mkdir -p build
  cd build

  msg "Starting cmake..."
  cmake ${srcdir}/${_svnmod} \
    -DCMAKE_INSTALL_PREFIX=${trinity_prefix} \
    -DCMAKE_VERBOSE_MAKEFILE=ON \
    -DWITH_DBSEARCHENGINE=ON \
    -DWITH_KCAL=ON \
    -DBUILD_ALL=ON
  make
}

package() {
  msg "Packaging - $pkgname-$pkgver"
#   cd ${srcdir}/${_svnmod}
  cd ${srcdir}/build

  make DESTDIR="$pkgdir/" install

#   rm -r ${srcdir}/${_svnmod}
}
