# Maintainer: Calvin Morrison <mutantturkey@gmail.com>

pkgname=trinity-kde-style-lipstik
pkgver=3513
pkgrel=1

arch=('i686' 'x86_64')

url='http://www.trinitydesktop.org'
license=('GPL')
groups=('trinity')
pkgdesc="Trinity - Lipstik style"

depends=('trinity-kdelibs' 'trinity-kdebase')
makedepends=('pkgconfig' 'autoconf' 'automake')

options=('libtool' '!strip')

source=(http://mirror.its.uidaho.edu/pub/trinity/releases/3.5.13/applications/kde-style-lipstik-3.5.13.tar.gz)
md5sums=('1585d4df7f0c68499fafa0b8ecb82dab')

_builddir=applications/kde-style-lipstik
_prefix=/opt/trinity

build() {

  msg "Setting Trinity Environment variables"
  export LD_LIBRARY_PATH=${_prefix}/lib:${_prefix}/lib/trinity:$LD_LIBRARY_PATH
  export PKG_CONFIG_PATH=${_prefix}/lib/pkgconfig:${QTDIR}/lib/pkgconfig

  msg "Starting autoconf..."
  cd "$srcdir/${_builddir}"
  cp -f "/usr/share/aclocal/libtool.m4" "admin/libtool.m4.in"
  cp -f "/usr/share/libtool/config/ltmain.sh" "admin/ltmain.sh"
  make -f admin/Makefile.common

  msg "Starting configure..."
  ./configure --with-qt-dir=${QTDIR} \
     --prefix=${_prefix} \
     --with-extra-includes=${_prefix}/include:/usr/include/dbus-1.0:${_prefix}/include/libkrandr \
     --with-extra-libs=${_prefix}/lib:${_prefix}/lib/trinity:${_prefix}/pkgconfig

  msg "Building ${pkgname}..."
  make
}

package() {
  msg "Packaging - $pkgname-$pkgver"
  cd ${srcdir}/${_builddir}
  make DESTDIR="${pkgdir}" install
}
