# Maintainer: Pawel 'l0ner' Soltys <pwslts@gmail.com>
# Contributor: Calvin Morrison <mutantturkey@gmail.com>

pkgname=trinity-kdegames
pkgver=3513
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.trinitydesktop.org'
license=('GPL')
groups=('trinity-extras')
pkgdesc="Trinity games collection"
depends=('trinity-kdelibs')
makedepends=('pkgconfig' 'autoconf' 'automake')
options=('libtool' '!strip')
source=(http://mirror.ets.kth.se/trinity/releases/3.5.13/kdegames-3.5.13.tar.gz)
md5sums=('6c63e161050939f53921bfceb923a19e')

_prefix=/opt/trinity

build() {

  msg "Setting Trinity Environment variables"
  export LD_LIBRARY_PATH=${_prefix}/lib:${_prefix}/lib/trinity:$LD_LIBRARY_PATH
  export PKG_CONFIG_PATH=${_prefix}/lib/pkgconfig:${QTDIR}/lib/pkgconfig

  #msg "Patching..."
  #patch -N -d ${srcdir} -p0 < kdegames.patch
  
  msg "Starting automake..."
  cd "$srcdir/kdegames"
  cp -f "/usr/share/aclocal/libtool.m4" "admin/libtool.m4.in"
  cp -f "/usr/share/libtool/config/ltmain.sh" "admin/ltmain.sh"
  make -f admin/Makefile.common

  msg "Starting configure..."
  ./configure --with-qt-dir=${QTDIR} \
     --prefix=${_prefix} \
     --enable-closure \
     --with-extra-includes=${_prefix}/include:/usr/include/dbus-1.0:${_prefix}/include/libkrandr \
     --with-extra-libs=${_prefix}/lib:${_prefix}/lib/trinity:${_prefix}/pkgconfig

  msg "Building ${pkgname}..."
  make
}

package() {
  msg "Packaging - $pkgname-$pkgver"
  cd ${srcdir}/kdegames
  make DESTDIR="${pkgdir}" install
}
