# Maintainer: Pawel 'l0ner' Soltys <pwslts@gmail.com>
# Contributor: Calvin Morrison <mutantturkey@gmail.com>

pkgname=trinity-kdeedu
pkgver=3513
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.trinitydesktop.org'
license=('GPL')
groups=('trinity-extras')
pkgdesc="Trinity educational applications and tools"
depends=('trinity-kdelibs')
makedepends=('pkgconfig' 'autoconf' 'automake')
options=('libtool' '!strip')
source=(http://mirror.ets.kth.se/trinity/releases/3.5.13/kdeedu-3.5.13.tar.gz)
md5sums=('37a14a67c4d00babc3015c6a31698cb0')

_prefix=/opt/trinity

build() {

  msg "Setting Trinity Environment variables"
  export LD_LIBRARY_PATH=${_prefix}/lib:${_prefix}/lib/trinity:$LD_LIBRARY_PATH
  export PKG_CONFIG_PATH=${_prefix}/lib/pkgconfig:${QTDIR}/lib/pkgconfig

  #msg "Patching..."
  #patch -N -d ${srcdir} -p0 < kdeedu.patch
  
  msg "Starting automake..."
  cd "$srcdir/kdeedu"
  cp -f "/usr/share/aclocal/libtool.m4" "admin/libtool.m4.in"
  cp -f "/usr/share/libtool/config/ltmain.sh" "admin/ltmain.sh"
  make -f admin/Makefile.common

  msg "Starting configure..."
  ./configure --with-qt-dir=${QTDIR} \
     --prefix=${_prefix} \
     --enable-closure \
     --with-extra-includes=${_prefix}/include:/usr/include/dbus-1.0:${_prefix}/include/libkrandr \
     --with-extra-libs=${_prefix}/lib:${_prefix}/lib/trinity:${_prefix}/pkgconfig

  msg "Building ${pkgname}..."
  make
}

package() {
  msg "Packaging - $pkgname-$pkgver"
  cd ${srcdir}/kdeedu
  make DESTDIR="${pkgdir}" install
}
