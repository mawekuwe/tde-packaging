# Maintainer: Pawel 'l0ner' Soltys <pwslts@gmail.com>
# Contributor: Calvin Morrison <mutantturkey@gmail.com>

pkgname=trinity-libkdcraw
pkgver=3513
pkgrel=2
arch=('i686' 'x86_64')
url='http://www.trinitydesktop.org'
license=('GPL')
groups=('trinity-extras')
pkgdesc="Trinity DCRaw library"
depends=('trinity-kdelibs')
         #openmpi
makedepends=('pkgconfig' 'autoconf' 'automake')
options=('libtool' '!strip')
source=(http://mirror.ets.kth.se/trinity/releases/3.5.13/libraries/libkdcraw-3.5.13.tar.gz)
md5sums=('770dd734dc427337a1a2e61b6b0874ae')

build() {
  msg "Setting Trinity Environment variables"
  [ "$QTDIR" = "" ] && . /etc/profile.d/qt3.sh
  [ "$TDEDIR" = "" ] && . /etc/profile.d/trinity.sh

  msg "Starting automake..."
  cd "$srcdir/libraries/libkdcraw"
  cp -f "/usr/share/aclocal/libtool.m4" "admin/libtool.m4.in"
  cp -f "/usr/share/libtool/config/ltmain.sh" "admin/ltmain.sh"
  sed -i -e "s@/usr/include/tqt@$TDEDIR/include/tqt@" admin/acinclude.m4.in
  make -f admin/Makefile.common

  msg "Starting configure..."
  ./configure --with-qt-dir=${QTDIR} \
     --prefix=${TDEDIR} \
     --enable-closure \
     --enable-openmp
     #--with-extra-includes=${TDEDIR}/include:/usr/include/dbus-1.0:${TDEDIR}/include/libkrandr \
     #--with-extra-libs=${TDEDIR}/lib:${TDEDIR}/lib/trinity:${TDEDIR}/pkgconfig

  msg "Building ${pkgname}..."
  make
}

package() {
  msg "Packaging - $pkgname-$pkgver"
  cd ${srcdir}/libraries/libkdcraw
  make DESTDIR="${pkgdir}" install
}
